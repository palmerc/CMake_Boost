cmake_minimum_required( VERSION 3.8 )

project( boost )

include( BuildBoost.cmake )
include( Lipo.cmake )

set( INSTALL_DIR ${CMAKE_BINARY_DIR}/boost )
set( INSTALL_DIR_LIB ${INSTALL_DIR}/lib )
set( INSTALL_DIR_INCLUDE ${INSTALL_DIR}/include )

set( SDKS iphoneos iphonesimulator )
set( BOOST_MODULES ${BOOST_STANDARD_MODULES} )
set( BOOST_TARGET_ARCHITECTURES_iphoneos armv7 armv7s arm64 )
set( BOOST_TARGET_ARCHITECTURES_iphonesimulator i386 x86_64 )

set( BITCODE_ENABLED on CACHE BOOL "" )
set( BUILD_SHARED off CACHE BOOL "" )

set( OVERRIDE_TIMESTAMP_CHECK off )

set( BOOST_BOOL_OPTIONS )
if( BUILD_FRAMEWORK )
    list( APPEND BOOST_BOOL_OPTIONS FRAMEWORK )
endif()
if( BUILD_SHARED )
    list( APPEND BOOST_BOOL_OPTIONS DYLIB )
endif()
if( BITCODE_ENABLED )
    list( APPEND BOOST_BOOL_OPTIONS BITCODE )
endif()

### Setup the modules to be built
set( BOOST_INCLUDES_STAMPS )
set( BOOST_INCLUDES )
set( BOOST_STATIC_LIBS )
set( BOOST_SHARED_LIBS )
foreach( BOOST_MODULE ${BOOST_MODULES} )
    set( BOOST_${BOOST_MODULE}_STATIC_LIB ${CMAKE_STATIC_LIBRARY_PREFIX}boost_${BOOST_MODULE}${CMAKE_STATIC_LIBRARY_SUFFIX} )
    set( BOOST_${BOOST_MODULE}_STATIC_INSTALL_PATH ${INSTALL_DIR_LIB}/${BOOST_${BOOST_MODULE}_STATIC_LIB} )
    if( BUILD_SHARED )
        set( BOOST_${BOOST_MODULE}_SHARED ${CMAKE_SHARED_LIBRARY_PREFIX}boost_${BOOST_MODULE}${CMAKE_SHARED_LIBRARY_SUFFIX} )
        set( BOOST_${BOOST_MODULE}_SHARED_INSTALL_PATH ${INSTALL_DIR_LIB}/${BOOST_${BOOST_MODULE}_SHARED} )
    endif()
endforeach( BOOST_MODULE )

foreach( SDK ${SDKS} )
    execute_process( COMMAND xcrun --sdk ${SDK} --show-sdk-path
            OUTPUT_VARIABLE XCODE_SDK_PATH
            OUTPUT_STRIP_TRAILING_WHITESPACE )

    set( PER_SDK_INSTALL_DIR ${INSTALL_DIR}/${SDK} )
    BuildBoost( SDK ${SDK}
            SDK_PATH ${XCODE_SDK_PATH}
            TARGET_ARCHITECTURES ${BOOST_TARGET_ARCHITECTURES_${SDK}}
            MODULES ${BOOST_MODULES}
            INSTALL_DIR ${PER_SDK_INSTALL_DIR}
            ${BOOST_BOOL_OPTIONS} )

    list( APPEND BOOST_INCLUDES_STAMPS ${BOOST_INCLUDES_STAMP_${SDK}} )
    list( APPEND BOOST_INCLUDES_DIR ${BOOST_INCLUDES_DIR_${SDK}} )
    foreach( BOOST_MODULE ${BOOST_MODULES} )
        list( APPEND BOOST_${BOOST_MODULE}_STATIC_LIBS ${BOOST_${BOOST_MODULE}_STATIC_LIB_${SDK}} )
        if( BUILD_SHARED )
            list( APPEND BOOST_${BOOST_MODULE}_SHARED_LIBS ${BOOST_${BOOST_MODULE}_SHARED_LIB_${SDK}} )
        endif()
    endforeach( BOOST_MODULE )
endforeach( SDK )

foreach( BOOST_MODULE ${BOOST_MODULES} )
    set( BOOST_${BOOST_MODULE}_STATIC_LIB ${CMAKE_STATIC_LIBRARY_PREFIX}boost_${BOOST_MODULE}${CMAKE_STATIC_LIBRARY_SUFFIX} )
    set( BOOST_INSTALL_PATH_${BOOST_MODULE}_STATIC ${INSTALL_DIR_LIB}/${BOOST_${BOOST_MODULE}_STATIC_LIB} )
    Lipo( INPUTS ${BOOST_${BOOST_MODULE}_STATIC_LIBS} OUTPUT ${BOOST_INSTALL_PATH_${BOOST_MODULE}_STATIC} )
    list( APPEND BOOST_STATIC_LIBS ${BOOST_INSTALL_PATH_${BOOST_MODULE}_STATIC} )

    if( BUILD_SHARED )
        set( BOOST_${BOOST_MODULE}_SHARED ${CMAKE_SHARED_LIBRARY_PREFIX}boost_${BOOST_MODULE}${CMAKE_SHARED_LIBRARY_SUFFIX} )
        set( BOOST_INSTALL_PATH_${BOOST_MODULE}_SHARED ${INSTALL_DIR_LIB}/${BOOST_${BOOST_MODULE}_SHARED} )
        Lipo( INPUTS ${BOOST_${BOOST_MODULE}_SHARED_LIBS} OUTPUT ${BOOST_INSTALL_PATH_${BOOST_MODULE}_SHARED} )
        list( APPEND BOOST_SHARED_LIBS ${BOOST_INSTALL_PATH_${BOOST_MODULE}_SHARED} )
    endif()
endforeach( BOOST_MODULE )

list( GET BOOST_INCLUDES_DIR 0 BOOST_INCLUDES )
set( BOOST_INCLUDES_STAMP ${INSTALL_DIR}/boost_include.stamp)
add_custom_command( OUTPUT ${BOOST_INCLUDES_STAMP}
        DEPENDS ${BOOST_INCLUDES_STAMPS}
        COMMAND ${CMAKE_COMMAND} -E copy_directory ${BOOST_INCLUDES} ${INSTALL_DIR_INCLUDE}
        COMMAND touch ${BOOST_INCLUDES_STAMP}
        COMMENT "Copying Boost ${BOOST_INCLUDES} to ${INSTALL_DIR_INCLUDE}" )

add_custom_target( boost_build ALL
        DEPENDS
            ${BOOST_INCLUDES_STAMP}
            ${BOOST_STATIC_LIBS}
            ${BOOST_SHARED_LIBS} )
